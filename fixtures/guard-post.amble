# ========================================
# Woodland: Abandoned Guard Post
# ========================================

room guard-post {
    name "Abandoned Guard Post"
    desc "A small stone guard post clings to the side of the trail, complete with a creaky wooden chair and a megaphone labeled [[red]]'USE ONLY IF VERY SURE'[[/red]]. A faded official-looking placard reads:\n[[box:NOTICE]] Passage beyond this point will have led to [[i]]anachronism.[[/i]][[/box]]\nIt seems no one’s been stationed here in [[i]]quite[[/i]] some time. The trail continues upward, leading to what appears to be an ancient road—and a distinctly non-ancient office park."
    overlay if item am_dollar {
        present r#"A silver coin sits upon a note on the kiosk: "Lunch Money - [[u]]don't[[/u]] get it from [[i]]that vendor[[/i]] again!" "#
        absent r#"A note on the kiosk reads: "Lunch Money - [[u]]don't[[/u]] get it from that vendor again!" The coin left a neat, circular void in the dust."#
    }
    exit "up to the road" -> inca-road
    exit "down the steps" -> two-sheds-landing
    scenery default "There's nothing remarkable about the {thing}."
    scenery "Wooden Chair"
}

# ========================================
# Scenery
# ========================================

item megaphone {
    name "Megaphone"
    desc "A typical white megaphone with [[bright-blue]][[b]]AMBLE Adventure Security[[/b]][[/bright-blue]] stenciled on one side. It appears to be broken."
    location room guard-post
    movability fixed "Stealing from security on your first day isn't a good look."
    visibility scenery
}

# ========================================
# Items
# ========================================

item am_dollar {
    name "Ankh-Morpork Coin"
    desc "A tarnished coin from the city-state of Ankh-Morpork."
    text "Royal Bank of Ankh-Morpork ~ One Dollar (maybe less)"
    movability free
    location room guard-post
    ability Read
}

# ========================================
# Triggers
# ========================================

trigger "[Guard-Post] Interesting Passers-By" only once
note "different characters depending on how they got past the fallen_tree"
when enter room guard-post {
    do schedule in 3 if all( chance 85%, has flag burned-fallen-tree )
    onFalse cancel
    note "smokey throws shade" {
        do show r#"Smokey Bear tromps past the guard post, growling "Nobody ever listens!" while glaring right at you."#
    }

    do schedule in 3 if all( chance 85%, has flag chopped-fallen-tree )
    onFalse cancel
    note "bunyan cleans up" {
        do show "Paul Bunyan gives you a nod as he lumbers by with Babe the Blue Ox, carrying the remains of the tree you chopped up."
        do add flag saw-paul-bunyan
    }
}
