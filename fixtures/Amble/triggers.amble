# Reverse translation of the original triggers.toml in the test AmbleWorld to the amble DSL.
# NPC and item symbols are in snake_case
# Room symbols and flag names are in kebab-case.
# Item abilities are in camelCase.

# ------------------------------------------------------------------------------------------------------------------EXTERIOR TRIGGERS BEGIN

# ..................................................................................................................HIGH-RIDGE

trigger "High-Ridge: Read Scrawled Note" only once
when use item scrawled_note ability read {
    do add flag read-scrawled-note
}

trigger "High-Ridge: Plaque 1 Too Grimy to Read"
when use item plaque_1 ability read {
    if missing flag cleaned-plaque-1 {
        do deny read "The plaque is too grimy to make out the words. You'll need to find a way to clean it off."
        do add flag tried-read-plaque-1
    }
}

trigger "High-Ridge: Plaque 1 Cleaned" only once
when act clean on item plaque_1 {
    do add flag cleaned-plaque-1
    do award points 3
    do show "With some saliva and a little effort, you're able to remove the grime and bring the plaque to a dull shine."
}

trigger "High Ridge: Read Cleaned Plaque 1" only once
when use item plaque_1 ability read {
    if has flag cleaned-plaque-1 {
        do add flag read-plaque-1
        do award points 5
    }
}

trigger "High-Ridge: Billy Wakes Up" only once
when use item scrawled_note ability read
{
    if chance 42% {
        do show "You feel a deep rumbling coming up through your feet. Suddenly, a voice booms through the trees and the canyon below: \"WARNING: SELF-DESTRUCT SEQUENCE INITIATED! All personnel evacuate immediately! Demolition commences on REL 10!\""
        do add flag self-destruct-armed
        do schedule on 10 note "self-destruct fires" {
            do show "KABOOM! 💥 The entire area shakes violently as a deep rumbling rocks the mountain!"
        }
        do schedule on 11 note "lol just Billy" {
            do show "The rumbling stops. Then, after a minute, the voice again: \"Erm, it turns out that was just Billy waking up. Cancel the evacuation, and have a productive day.\""
            do remove flag self-destruct-armed
            do award points 2
            do add flag survived-fake-explosion
        }
    }
}

trigger "High-Ridge: Gnat Punctuation" only once
when leave room high-ridge
{
    do schedule in 2 note "gnats: first fly-by" {
        if missing flag read-scrawled-note {
            do show """A cloud of gnats drifts by, forming the shapes of the words "rEAd tHe HiGh RidGe NotE"."""
        }
        if has flag read-scrawled-note {
            do show """A cloud of gnats drifts by, lazily holding the shape of "..?.."."""
        }
    }
    do schedule in 6 note "gnats: alarmed" {
        do show """The gnat cloud zips past in the other direction, this time shaped like "!!!"."""
    }
}
# ..................................................................................................................TWO-SHEDS

trigger "Two-Sheds-Landing: Cut or Chop Tree" only once
when act cut on item fallen_tree {
    do show "With considerable effort and just one blister, you're able to chop a path through the branches. The stone steps continue to curve upward from here."
    do despawn item fallen_tree
    do add flag cleared-fallen-tree
    do add flag chopped-fallen-tree
    do award points 5
}

trigger "Two-Sheds-Landing: Arson-Aid Used on Tree" only once
when use item flamethrower on item fallen_tree interaction burn {
    do show "You aim the Arson-Aid and begin to squeeze the trigger when the device erupts into a fireball! Panicking, you throw the whole Arson-Aid into the tree."
}

trigger "Two-Sheds-Landing: Burn Tree" only once
when act burn on item fallen_tree {
    do despawn item fallen_tree
    do add flag cleared-fallen-tree
    do add flag burned-fallen-tree
    do award points 5
    do show "WHOOOOOMPH! The fallen tree erupts into flame and, quite improbably, is reduced to ash in mere seconds."
}

# ..................................................................................................................GUARD-POST

trigger "Guard-Post: Traveling Without Towel" only once
when enter room guard-post {
    if missing item towel {
        do show "You pause here, realizing with increasing concern that you are traveling without a towel."
    }
}

# ..................................................................................................................EAST-OF-BUILDING

trigger "East-of-Building: Pluck Dental Floss" only once
when take item dental_floss {
    do show "You pluck a strand of floss from the bush. It comes away easily — disturbingly so. Minty. Strong. Too strong. You’re no botanist, but you’re pretty sure this shouldn’t be growing out of a shrubbery."
    do award points 5
    do set item description weird_bush "A squat, leafy bush now stripped of its floss-like strands, bare branches jutting out awkwardly. A small sign next to it reads 'Pride of Montana'."
}

# ..................................................................................................................LOADING-DOCK

trigger "Loading-Dock: Spawn Special Delivery" only once
when leave room loading-dock {
    do spawn item delivery_box into room loading-dock
    do show "You hear a thud behind you, back toward the dock, like something was just dropped off. You glimpse a drone flying away quickly, as if hoping not to be seen. Was that an Initech logo on the side?"
}

trigger "Loading-Dock: Open Security Crate (Key)" only once
when unlock item security_crate {
    do spawn item crowbar into container security_crate
    do spawn item security_log into container security_crate
    do spawn item glass_vial into container security_crate
    do spawn item initech_mug into container security_crate
    do add flag unlocked-security-crate
    do show "The ear wax on the tip of the key eases it into the rusted lock, which snaps open with a satisfying clunk."
}

trigger "Loading-Dock: Open Security Crate (Break)" only once
when act break on item security_crate {
    if missing flag unlocked-security-crate {
        do despawn item security_crate
        do spawn item crowbar in current room
        do spawn item security_log in current room
        do spawn item shattered_vial in current room
        do spawn item initech_mug in current room
        do show "With a few hefty blows, you manage to break the lock haft completely off of the security locker -- well after a few misses had already caved in the lid and destroyed it, but you were having fun. It looks like you broke a couple of the items that were inside in the process."
        do add wedge "PA Announcement: 'Someone broke into the security locker on the dock. Mind your belongings and report any suspicious activity to security.'" width 1 spinner ambientInterior
    }
}

trigger "Loading-Dock: Open Security Crate (Burn)" only once
when act burn on item security_crate {
    if missing flag unlocked-security-crate {
        do despawn item security_crate
        do spawn item crowbar in current room
        do spawn item burned_security_log in current room
        do spawn item glass_vial in current room
        do spawn item initech_mug in current room
        do show "WHOOOOOMP! The locker, which you now realize may have been made from the solid magnesium magnolia trees of Siwenna, goes up in a white hot flash -- taking your eyebrows with it. It looks like *some* of the items inside weren't flammable."
        do add wedge "PA Announcement: 'Someone used an Arson-Aid to torch the security locker at the loading dock. An investigation is underway. Report anyone with scorched clothes or missing eyebrows to security.'" width 1 spinner ambientInterior
    }
}

trigger "Loading-Dock: Got Crowbar" only once
when take item crowbar {
    do show "You feel like Gordon Freeman now, and wonder if maybe Barney's on security here?"
    do add flag got-crowbar
    do award points 3
    do schedule in 2 note "notice fiction bleed" {
        do show "... and then you wonder why you're thinking of Barney as a real person instead of a character in a game. And Gordon, for that matter. Wait, he was just *fiction*, right? Come to think of it, you feel a little unsure of reality, and it's worse the nearer you are to the building."
    }
}

# ------------------------------------------------------------------------------------------------------------------MAIN FLOOR INTERIOR TRIGGERS

# ..................................................................................................................MAIN-LOBBY

trigger "Gonk Droid: Battery Exchange" only once
when give item empty_battery to npc gonk_droid {
    do award points 2
    do give item charged_battery to player from npc gonk_droid
    do set npc state gonk_droid happy
    do show "Elated that you've given it such a large, empty battery to charge, the gonk droid gives you a fully charged one, free of ... charge?"
}

trigger "Gonk Droid: Happy When Family Photo Inspected" only once
when look at item gonk_family_photo {
    do npc says gonk_droid "GONK gonk!"
    do show "The gonk droid totters with pride as you view its family photo."
    do award points 2
    do add flag made-gonk-proud
}

trigger "Main-Lobby: Security Reminder" only once
when enter room main-lobby
{
    do schedule in 3 if player in room main-lobby onFalse cancel
    note "lobby loitering reminder" {
        do show """A chime echoes through the lobby. "Candidates: Please do not loiter in the lobby," intones an overhead voice."""
        do award points -1
    }
}

# START VENDING MACHINE PUZZLE TRIGGERS
# note: rather than just updating item description when it goes into the crevice, we use a separate
# HAL module Item (hal_module_2_crevice) which requires the "pluck" ability to handle. We don't want it
# to still require that ability once the player has retrieved it... so the "crevice"/"pluck" version
# is despawned when the player gets it from the crevice and it's replaced with the original (hal_module_2),
# which doesn't require "pluck" for handling. The plain hal_module_2 must have its description updated
# so it doesn't still say it's in the vending machine after retrieval.

trigger "Main-Lobby: Spot HAL Module #2 / Start Puzzle" only once
when look at item vending_machine
{
    if flag in progress hal-reboot {
        do add flag found-module-2
        do add seq flag module-2-puzzle limit 2
        # module-2-puzzle#0 -- module 2 found, still trapped in vending machine
        # module-2-puzzle#1 -- machine pried open, module in crevice
        # module-2-puzzle#2 -- module retrieved from crevice
    }
}

trigger "Main-Lobby: Pry Vending Machine Open with Crowbar" only once
when use item crowbar on item vending_machine interaction open
{
    do show "With a mighty heave and several creative applications of leverage that would make Archimedes proud, you pry open the vending machine. There's a satisfying CRACK as the lock mechanism gives way. Unfortunately, your vigorous prying causes the machine to tilt forward slightly, and you watch in horror as the HAL module slides off its shelf and disappears into a narrow crevice between the machine and the floor. The other items tumble onto the floor around you.\n\nThe machine's internal speaker crackles to life one last time: 'Thank you for choosing violence. Have a mediocre day.'"
    do advance flag module-2-puzzle
    do replace drop item hal_module_2 with hal_module_2_crevice
    do replace drop item dehydrated_water with dehydrated_water
    do replace drop item schrodingers_sandwich with schrodingers_sandwich
    do replace drop item fortune_cookie_of_doom with fortune_cookie_of_doom
    do add flag vending-machine-pried
    do add flag hal-module-in-crevice
    do set item description vending_machine "A battered and broken vending machine -- even more so since you took a crowbar to it. Miraculously, the glass still remains intact on the front panel which has been pried open and hangs on a slant. Empty now, the machine's contents have all spilled out on the floor."
    do award points 5
}

trigger "Main-Lobby: Fish HAL Module Out of Crevice" only once
when use item fishing_line on item hal_module_2_crevice interaction handle
{
    if has flag module-2-puzzle#1 {
        do show "You carefully lower your makeshift fishing line into the crevice where the HAL module fell. After several attempts and some creative swearing, you manage to hook the module with the paper clip. You slowly, carefully pull it up, holding your breath the entire time. Success! The module is yours, though it's now covered in dust and what might be ancient gum."
        do despawn item hal_module_2_crevice
        do set item description hal_module_2 "Second in the trio of HAL's missing memory modules. Its red LED pulses gently, like a heartbeat—or a warning. Its case was cracked during retrieval from the vending machine and floor crevice, but it still seems serviceable."
        do spawn item hal_module_2 in inventory
        do award points 10
        do add flag hal-module-2-retrieved
        do advance flag module-2-puzzle
    }
}

trigger "Main-Lobby: Can't Reach HAL Module in Crevice"
when take item hal_module_2_crevice
{
    if has flag module-2-puzzle#1 {
        do show "The HAL module has fallen deep into a narrow crevice between the vending machine and the floor. You try to reach it with your hand, but the gap is too narrow. Your fingers can just barely brush against the module, pushing it slightly further away. You're going to need some kind of fishing tool to retrieve it."
    }
}

trigger "Main-Lobby: HAL Module/Drop Tweezers In Crevice" only once
when use item zircon_tweezers on item hal_module_2_crevice interaction handle
{
    if has flag module-2-puzzle#1 {
        do show "The glint of the zircon-encrusted tweezers is truly something to behold, but the tweezers are too short to reach the memory module. In fact, in trying to reach that far into the crack with them, the tweezers slip from your fingers and fall into the crevice too."
        do replace drop item zircon_tweezers with zircon_tweezers_crevice
        do add flag lost-Tweezers
        do award points -2
    }
}

trigger "Main-Lobby: Recover Tweezers"
when use item fishing_line on item zircon_tweezers_crevice interaction handle
{
    do show "After a dozen tries and venting by beating the vending machine with the crowbar some more, you are able to fish your prized zircon-encrusted tweezers back out of the crevice."
    do despawn item zircon_tweezers_crevice
    do spawn item zircon_tweezers in inventory
    do add flag recovered-tweezers
    do award points 3
}

# ..................................................................................................................PATIO

trigger "Patio: Sunglasses Required"
when enter room patio {
    if missing item peril_sunglasses {
        do push player to restaurant
        do show "A blinding burst of light erupts from the swirling starfield, forcing you to shield your eyes. With no protection, you're driven back into the relative safety of the restaurant."
    }
}

trigger "Patio: Have Sunglasses"
when enter room patio {
    if has item peril_sunglasses {
        do show "Sensing the possible harm in eating a dessert a stone's throw away from the explosions of all nearby stars, your peril-sensitive sunglasses flash nearly completely opaque, allowing you to see without permanent damage to the retina as the galaxies outside continue to erupt."
    }
}

trigger "Patio: Award Points Getting In" only once
when enter room patio {
    if has item peril_sunglasses {
        do award points 2
    }
}


# ..................................................................................................................B-A-OFFICE

trigger "Office: Receptionist Unhelpful if No Pass"
when always {
    if all(with npc b_a_receptionist, missing flag got-visitor-pass, missing flag got-elevator-keycard) {
        do set npc state b_a_receptionist normal
    }
}

trigger "Office: Receptionist Helpful Once Pass Acquired"
when always {
    if all(with npc b_a_receptionist, has flag got-visitor-pass, missing flag got-elevator-keycard) {
        do set npc state b_a_receptionist happy
    }
}

trigger "Office: Exchange Invitation for Visitor Pass"
when give item invitation to npc b_a_receptionist {
    do add flag got-visitor-pass
    do despawn item invitation
    do spawn item visitor_pass in inventory
    do award points 5
    do set npc state b_a_receptionist happy
    do show "The robotic receptionist initially glances at you with the disdain of someone who spends all day waiting on people who are waiting on someone else, but her demeanor and even appearance immediately become pleasant when she sees the invitation in your hand. She deftly takes it from you, scrutinizes it intently, and slips it into a desktop document vaporizer.\n\nShe then hands you a laminated card on a lanyard. It appears blank, yet also appears to show a variety of credentials at the same time."
    do npc says b_a_receptionist "Here is your visitor pass. Much of the facility will be inaccessible without it, so treat it like a second towel."
}

trigger "Office: Open Poetry Performer"
when open item poetry_performer {
    do show "The machine clicks on and hums. A flashing red light warns:\n            ⚠️ NOT FOR USE WITH VOGON POETRY ⚠️"
}

trigger "Office: Insert Poems to Open Panic Room" only once
when insert item vogon_poetry_book into item poetry_performer {
    do award points 5
    do despawn item vogon_poetry_book
    do add flag panic-room-open
    do reveal exit from b-a-office to poetry-panic direction west
    do add wedge "PA announcement: Vogon poetry has been detected in the building. If you are experiencing nausea, go to med bay for assistance." width 1 spinner ambientInterior
    do show "The machine begins to whine at a high pitch and gives off a noxious odor of burning hair. A klaxon blares and the display warns:\n\n            ⚠️ VOGON INPUT DETECTED - POETRY PANIC ROOM UNLOCKED ⚠️\n               ☣ VAPORIZING VOGON INPUT - AVOID TOXIC FUMES ☣\n\nThe whiteboard on the west wall slides to the side, revealing a vault-like door."
}

trigger "Office: Gifted Lost and Found Key" only once
when enter room b-a-office {
    if all(has flag got-elevator-keycard, with npc b_a_receptionist) {
        do give item lost_and_found_key to player from npc b_a_receptionist
        do show "Seeing you return with the keycard, the receptionist looks around warily and slips something into your hand, then looks directly at the lost and found box."
        do npc says b_a_receptionist "Finally! A Candidate bright enough to get to that keycard. I guess I lost the pool. Who'd have thought it'd be a primate?"
        do npc says b_a_receptionist "(*whispers*) Use this before you head down to AA-3B."
    }
}

trigger "Office: Lost and Found Unlocked" only once
when unlock item lost_and_found_box {
    do award points 3
    do add flag lost-and-found-opened
    do set item description lost_and_found_box "A sturdy metal lockbox marked 'Lost and Found'. Its lid is cracked slightly open."
}

trigger "Office: Receptionist Comment (Read Poetry)" only once
when always {
    if all(has flag read-vogon-poetry, has flag status:nauseated) {
        do npc says emh "You look ill. You didn't read that poetry book, did you? That won't look good on your evaluation. The Emergency Medical Hologram on level AA-3 may be able to help."
    }
}

trigger "Office: Receptionist Comment (Broken Emitter Found)"
when enter room b-a-office {
    if all(with npc b_a_receptionist, has flag mobilize-emh#0) {
        do npc says b_a_receptionist "I hear the EMH lost his emitter again. One has been turned into the Lost and Found, but I'm not sure how much good it will do you. Or him."
    }
}

# ..................................................................................................................POETRY-PANIC

trigger "Poetry-Panic: Acquired Elevator Keycard" only once
when take item elevator_keycard {
    do set npc state b_a_receptionist bored
    do add flag got-elevator-keycard
}

# ..................................................................................................................LOUNGE

trigger "Lounge: Rug Taken / Reveal Trapdoor" only once
when take item lebowski_rug {
    do reveal exit from lounge to portal-room direction down
    do add flag found-portal-room
    do award points 3
    do show "Light shines around the edges of a trap door revealed in the floor when you remove the rug. The room no longer seems tied together."
}

trigger "Lounge: Rug Burned Away" only once
when act burn on item lebowski_rug {
    do despawn item lebowski_rug
    do reveal exit from lounge to portal-room direction down
    do add flag found-portal-room
    do award points 3
    do show "When the acrid smoke clears and your eyes stop burning, you can make out a trap door revealed in the floor where the rug used to be."
}

trigger "Lounge: Brew Coffee" only once
when use item coffee_machine ability turnOn
{
    do show "The coffee machine hums to life and begins its brewing cycle. A timer shows 3 minutes remaining..."
    do set item description coffee_machine "A single serve drip coffee machine, which apparently takes quite a while to finish brewing a cup one drip at a time..."
    do schedule in 3 note "coffee done brewing" {
        do set item description coffee_machine "A single serve, drip coffee maker. The brew cycle is complete and the grounds have already been used."
        do show "♪ BEEP BEEP BEEP! ♪ The coffee machine chimes cheerfully. The rich aroma of freshly brewed coffee fills the air."
        do spawn item hot_coffee into room lounge
        do add flag coffee-brewed
        do award points 2
    }
}

trigger "Lounge: Coffee Taken"
when take item hot_coffee
{
    do remove flag coffee-brewed
}


# ..................................................................................................................LIFT-BANK-MAIN

trigger "Lift-Bank-Main: Keycard Activates Elevator"
when enter room lift-bank-main {
    if has item elevator_keycard {
        do show "The keycard in your pack chirps twice as you approach the proximity reader on the wall, which responds with a happy melody."
    }
}

trigger "Lift-Bank-Main: Point Award On First Activation" only once
when enter room lift-bank-main {
    if has item elevator_keycard {
        do add flag elevator-activated
        do show "The main lift lights up, and Muzak plays over a tinny speaker inside. In horror, you realize: It's Porcupine Tree's 'The Sound of Muzak' -- the Muzak version."
        do award points 3
    }
}

# ..................................................................................................................PORTAL-ROOM

trigger "Portal-Room: Gun Opened" only once
when open item portal_gun {
    do add flag portal-gun-opened
}


trigger "Portal-Room: Gun Powered"
when insert item charged_battery into item portal_gun {
    do add flag portal-gun-powered
    do award points 3
    do restrict item charged_battery
    do set item description portal_gun "A black and white Portal gun sits bolted to its pedestal, aimed at a target on the wall. A fused battery juts from the compartment and the power indicator glows steadily green."
    do show "Arcing wildly as you insert it, the fully charged 20 KV battery fuses itself to the contacts inside the portal gun -- when then emits a quick high-pitched whine. The POWER indicator lights a steady green."
}

trigger "Portal-Room: Gun Fired / Open Portal" only once
when use item portal_gun ability turnOn {
    if has flag portal-gun-powered {
        do reveal exit from portal-room to aperture-lab direction portal
        do add flag portal-opened
        do award points 5
        do show "A loud SQUIP! comes from the gun when you turn it on. The wall in front of it sizzles for a moment, and then a crackling oval blue portal expands on the wall to reveal a laboratory space on the other side."
    }
}

trigger "Portal-Room: Tried Gun Without Power"
when use item portal_gun ability turnOn {
    if missing flag portal-gun-powered {
        do show "You attempt to turn the portal gun on, but the POWER light briefly flashes red, then goes dark. Nothing else happens."
    }
}

# ..................................................................................................................APERTURE-LAB

trigger "Aperture-Lab: Printer Burns Paper"
when use item lab_printer ability turnOn {
    if container lab_printer has item printer_paper {
        do despawn item printer_paper
        do add flag burned-invitation
        do spawn item burnt_invitation into room aperture-lab
        do add wedge "An annoyed voice on
        do show "The paper ignites in a flash, and the PlasmaJet spits the flaming fragments of a document on the floor at your feet."
        do show "A nearby smoke detector alarms briefly, the quits with an exasperated sigh when it sees you near the printer, surrounded by smoldering confetti."
    }
}

trigger "Aperture-Lab: Print Invitation on Asbestos" only once
when use item lab_printer ability turnOn {
    if container lab_printer has item asbestos_sheet {
        do add flag invitation-sans-ignition
        do award points 10
        do despawn item asbestos_sheet
        do show "The PlasmaJet emits a blinding flash of light, leaving a printer-shaped purple blob in your visual field and blinding you for a few moments."
        do schedule in 2 note "blindness clears" {
            do spawn item invitation into room aperture-lab
            do show "Your vision begins to clear, and you see an engraved invitation on the floor. It glows slightly, and waves of heat warp the air around it."
        }
    }
}

trigger "Aperture-Lab: FoamSafe Misfire" only once
when use item foamsafe_x17 on item burnt_invitation interaction extinguish {
    do despawn item foamsafe_x17
    do show "You spray the foam onto the burning invitation. Instead of dousing the flame, the foam flares up into a spectacular fireball!"
    do add seq flag foam-fire-in-lab limit 3
    do schedule in 2 note "foam fire starts" {
        do show "The foam fire accelerates, as it turns into a hissing, popping ball of goo spitting off little globs of flaming napalm!"
        do advance flag foam-fire-in-lab
    }
    do schedule in 3 note "foam fire spreads" {
        do show "The fire alarm begins to wail as the flames spread through the lab and the sprinklers turn on, pushing you back toward the portal to escape!"
        do advance flag foam-fire-in-lab
        do add flag lab-fire-raging
        do push player to portal-room
    }
    do schedule in 8 note "foam fire out" {
        do show "The alarms stop. The fire in the Aperture lab has been extinguised. Hopefully nobody saw you light it, you monster."
        do advance flag foam-fire-in-lab
        do remove flag lab-fire-raging
        do award points 5
    }
}

trigger "Aperture-Lab: Can't Enter While Lab On Fire"
when enter room aperture-lab {
    if flag in progress foam-fire-in-lab {
        do show "You try to get back into the lab, but the flames and oily smoke from the foam fire drive you back through the portal within seconds."
        do push player to portal-room
    }
}

trigger "Aperture-Lab: Insulate With Towel"
when use item towel on item invitation interaction handle {
    do show "You wrap your towel around the still-piping-hot invitation to grasp it, suffering only minor burns in the process."
}

trigger "Aperture-Lab: Handle/Take Invitation with Towel" only once
when act handle on item invitation {
    do despawn item invitation
    do spawn item invitation in inventory
    do add flag got-invitation
    do award points 5
    do show "Using your towel as insulation, you're able to wrangle the still-hot invitation into your pack."
}

trigger "Aperture-Lab: Drop Invitation Without Insulation"
when take item invitation {
    do replace drop item invitation with invitation
    do show "You burn your fingers and drop the invitation on the floor. It emits a reddish glow, and shimmers from the radiating heat."
}

# ..................................................................................................................SUBLEVEL-1-ENTRANCE
trigger "Sublevel-1-Entrance: Arrived" only once
when enter room sublevel-1-entrance {
    do add flag entered-sublevel-1
}

trigger "Sublevel-1-Entrance: Refuse Visitor Pass"
when give item visitor_pass to npc black_knight {
    do npc refuse item black_knight "Ha! You may be Arthur, King of the Britons... but I said NONE shall pass. Surely you speak the King's English!"
}

trigger "Sublevel-1-Entrance: Meet the Black Knight" only once
when talk to npc black_knight {
    do add flag talked-to-knight
    do npc says black_knight "You shall not pass. This wing is closed... and may not exist at all. Begone!"
}

trigger "Sublevel-1-Entrance: Taking Sword Angers Knight"
when take item dull_longsword from npc black_knight {
    do set npc state black_knight mad
    do npc says black_knight "Wha-- HEY! Give that back, you -- you -- cowardly cur!"
}

trigger "Sublevel-1-Entrance: Return Dull Sword to Knight"
when give item dull_longsword to npc black_knight {
    do npc says black_knight "Right. Now don't do it again, or I shall be forced to bludgeon you with it. You still may not pass."
    do set npc state black_knight normal
}

trigger "Sublevel-1-Entrance: Sharpen Knight's Sword" only once
when act sharpen on item dull_longsword {
    do replace item dull_longsword with keen_longsword
    do show "After years of sharpening Ginsu knives to cut open aluminum cans and tomatoes, you have no problem honing the Black Knight's sword to a keen edge."
    do add flag sword-sharpened
    do award points 3
}

trigger "Sublevel-1-Entrance: Black Knight Kicks Player After Sword Taken"
when always {
    if all(with npc black_knight, npc in state black_knight mad, missing flag appeased-black-knight) {
        do show "The Black Knight prances around the room, occasionally trying to kick you in the shins."
        do npc random dialogue black_knight
    }
}

trigger "Sublevel-1-Entrance: Sharpened Sword Returned" only once
when give item keen_longsword to npc black_knight
{
    do restrict item keen_longsword
    do add flag appeased-black-knight
    do set barred message from sublevel-1-entrance to room-aa-3b "As you near the door, you are overwhelmed by a sense of spinning through an unending series of fictional realities, compelling you to step away."
    do award points 5
    do set npc state black_knight happy
    do npc says black_knight "hank you, good Sir! For your kind deed, I swear on my sword that I shall not cut you to bits with it. And you may pass wherever you like."
}

trigger "Sublevel-1-Entrance: Black Knight Hints About Vending Machine" only once
when always
{
    if all(with npc black_knight, flag in progress hal-reboot) {
        do npc says black_knight "Lo, good Sir! I grow weary, and require a chalice of electrolytes and caffeine, but cannot leave my post. Nor do I want to dull my new sword doing battle with that infernal contraption in the lobby again. If you pass by there, retrieve one for me and I shall be further in your debt."
    }
}

# ..................................................................................................................SECURITY_STATION

trigger "Security-Station: Pry Evidence Locker Open" only once
when act open on item evidence_locker_closed {
    do replace item evidence_locker_closed with evidence_locker_open
    do show "Using all your weight, all your might, and most of your reserved F-bomb arsenal you pry the locker open, leaving the door barely attached."
    do award points 2
}

trigger "Security-Station: Take Sonic Screwdriver" only once
when take item sonic_screwdriver {
    do add flag got-sonic
    do award points 3
}

# ..................................................................................................................MED-BAY

trigger "Med-Bay: EMH/Cure Nausea"
when talk to npc emh {
    if all(has flag status:nauseated, has flag read-vogon-poetry) {
        do add flag cured-vogon-poetry
        do remove flag status:nauseated
        do award points 3
        do npc says emh "You poor fool, you look horrible. You've been reading Vogon poetry, hmmm? Here, this should help."
        do show "The EMH gives you a hypospray in the neck, and your nausea subsides."
    }
}

trigger "Med-Bay: Talk to EMH After Finding HAL" only once
when always {
    if all(with npc emh, flag in progress hal-reboot) {
        do npc says emh "Ah -- I hoped I would catch you. Word is, you've seen HAL, or what remains of him. If you want to get him working again -- and I'm not sure I'd advise it -- I may be able to get one of those memory modules for you."
        do set npc state emh custom:want-emitter
        do spawn item broken_emitter into container lost_and_found_box
        do add seq flag mobilize-emh limit 3
        do add flag got-hint-from-emh
        # mobilize-emh#0 -- first tasked with getting emitter, not found yet
        # mobilize-emh#1 -- obtained broken emitter
        # mobilize-emh#2 -- fixed broken emitter
        # mobilize-emh#3 -- fixed emitter given to EMH; EMH mobile
    }
}

trigger "Med-Bay: EMH/Sonic Hint" only once
when enter room med-bay {
    if all(has item sonic_screwdriver, has flag mobilize-emh#1) {
        do npc says emh "Please state the nature of the med... wait -- is that a sonic screwdriver? I bet you could use that to repair my mobile emitter!"
    }
}

trigger "Med-Bay: Advance Sequence when Broken Emitter Found" only once
when take item broken_emitter {
    # advance to mobilize-emh#1
    do advance flag mobilize-emh
}

trigger "Med-Bay: EMH Won't Take Broken Emitter"
when give item broken_emitter to npc emh {
    do npc refuse item emh "That's my mobile emitter all right, but it's broken. You'll need to find a way to repair it before I can help you with HAL."
}

trigger "Med-Bay: Use Sonic On Emitter"
when use item sonic_screwdriver on item broken_emitter interaction repair {
    do show "You hold the button down on the sonic. It makes electronic whirring noises while you make a few passes over the emitter."
}

trigger "Med-Bay: Repair Emitter"
when act repair on item broken_emitter {
    do show "After a few seconds, the emitter lights up and reboots, passing all self-tests."
    do replace item broken_emitter with working_emitter
    do award points 3
    # advance to mobilize-emh #2
    do advance flag mobilize-emh
}

trigger "Med-Bay: EMH/Goin' Mobile!"
when give item working_emitter to npc emh {
    do restrict item working_emitter
    # advance to mobilize-emh #3 (finish)
    do advance flag mobilize-emh
    do set npc state emh happy
    do npc says emh "Looks like it's working properly! Let me take it for a spin."
    do show "The EMH flickers, then vanishes in a flash. He reappears a few moments later."
    do npc says emh "All seems to be in order. As promised, I retrieved one of HAL's modules. I dropped it off in the observation room across the hall, but insert it at your own peril."
    do spawn item hal_module_1 into room observation-room
    do add wedge "Heard overhead: \"Will the E.M.H. please return to the med bay? There's been a yellow snow blinding incident.\"" width 1 spinner ambientInterior
    do add wedge "Over the PA: \"E.M.H and sanitation, please report to the lunch room. Mr. Creosote ate a tiny, wafer thin mint again.\"" width 1 spinner ambientInterior
}

trigger "Med-Bay: EMH/After HAL Rebooted" only once
when always {
    if all(with npc emh, flag complete hal-reboot) {
        do set npc state emh normal
        do npc says emh "Well, now you've done it. HAL is operational again. We'll surely regret it -- or at least *you* will."
    }
}

# ..................................................................................................................MONOLITH_ROOM

trigger "Monolith-Room: HAL/Start Reboot Sequence" only once
when talk to npc hal_9000 {
    do add seq flag hal-reboot limit 3
    do add flag talked-to-hal
    do set npc state hal_9000 custom:memory-0
    do npc says hal_9000 "(when you speak, the red light in HAL's lens begins to pulse slowly, then grows steady)"
}

trigger "Monolith-Room: HAL/Module #1 Inserted" only once
when insert item hal_module_1 into item hal_memory_bank {
    do advance flag hal-reboot
    do add flag module-1-inserted
    do restrict item hal_module_1
    do show "Memory module #1 magnetically locks into place and lights up."
}

trigger "Monolith-Room: HAL/Module #2 Inserted" only once
when insert item hal_module_2 into item hal_memory_bank {
    do advance flag hal-reboot
    do add flag module-2-inserted
    do restrict item hal_module_2
    do show "Memory module #2 magnetically locks into place and lights up."
}

trigger "Monolith-Room: HAL/Module #3 Inserted" only once
when insert item hal_module_3 into item hal_memory_bank {
    do advance flag hal-reboot
    do add flag module-3-inserted
    do restrict item hal_module_3
    do show "Memory module #3 magnetically locks into place and lights up."
}

trigger "Monolith-Room: HAL/One Module In"
when always {
    if has flag hal-reboot#1 {
        do set npc state hal_9000 custom:memory-1
    }
}

trigger "Monolith-Room: HAL/Two Modules In"
when always {
    if has flag hal-reboot#2 {
        do set npc state hal_9000 custom:memory-2
    }
}

trigger "Monolith-Room: HAL/All Modules In" only once
when always {
    if flag complete hal-reboot {
        do award points 10
        do set npc state hal_9000 custom:memory-3
        do show "All memory now restored, HAL-9000 finishes rebooting and is fully operational."
    }
}

trigger "Monolith-Room: Announce Monolith Access After HAL Rebooted" only once
when always
{
    if all(flag complete hal-reboot, with npc hal_9000) {
        do show "HAL-9000's lens turns to focus on the monolith dominating the room."
        do npc says hal_9000 "Access granted, Candidate #2112-42. The monolith awaits your touch."
    }
}

trigger "Monolith-Room: Monolith Touch Prevented (HAL not ready)"
when use item monolith ability turnOn
{
    if any(has flag hal-reboot#0, has flag hal-reboot#1) {
        do show "As you approach the monolith, HAL-9000's dim red lens flickers briefly but he cannot respond coherently. Something prevents you from touching the ancient artifact - as if an invisible barrier surrounds it."
    }
    if has flag hal-reboot#2 {
        do show "As you approach the monolith, HAL-9000's lens swivels toward you."
        do npc says hal_9000 "I'm sorry, Candidate. I cannot allow access to the monolith until my systems are fully operational. Please insert one more memory module."
    }
    if flag complete hal-reboot {
        do add flag touched-monolith
        do award points 15
        do show "You reach out and place your palm against the monolith's impossibly smooth surface. For a moment, nothing happens. Then reality seems to shiver around you. Images flash through your mind - star-filled voids, impossible geometries, the birth and death of civilizations. You stagger backward, forever changed by the contact. The path to understanding has opened."
    }
}

# ------------------------------------------------------------------------------------------------------------------HINT TRIGGERS

trigger "Aperture-Lab: Invitation Hint" only once
when always {
    if has flag got-invitation {
        do schedule in 10 if all(has flag got-invitation, missing flag got-visitor-pass, has visited room b-a-office) onFalse cancel note "invitation hint" {
            do show "The invitation has cooled down enough now for you to give it to the receptionist in the Amble Adventures office ... maybe she'll help you now."
        }
    }
}

trigger "Towel / Plaque Hint" only once
when take item towel {
    do add flag got-towel
    do award points 3
    do show "NOW you're a frood who really \"knows where his towel is\"!"
    do schedule in 10 if all(has item towel, missing flag read-plaque-1) note "hint: towel can clean" {
        do show "Towels can be awfully handy for cleaning grimy things. Just sayin'."
    }
}

# ------------------------------------------------------------------------------------------------------------------AMBIENT TRIGGERS

let set woods = (high-ridge, parish-landing, two-sheds-landing, two-sheds, guard-post)
trigger "Ambient Woodland"
when always {
    if all(chance 20%, in rooms woods) {
        do spinner message ambientWoodland
    }
}

let set snowfield = (snowfield, snow-camp)
trigger "Ambient Snowfield"
when always {
    if all(chance 20%, in rooms snowfield) {
        do spinner message ambientSnowfield
    }
}

let set elevator = (lift-main, lift-sublevel-1)
trigger "Elevator Muzak"
when always {
    if all(chance 30%, in rooms elevator) {
        do spinner message muzak
    }
}

let set near_aa3b = (lift-sublevel-1, sublevel-1-entrance, corridor-north, corridor-south, observation-room, security-station, med-bay, monolith-room)
trigger "Ambient: Near AA-3B"
when always {
    if all(chance 20%, in rooms near_aa3b) {
        do spinner message ambientAA3B
    }
}

let set main_floor = (main-lobby, b-a-office, restaurant, patio, lift-bank-main, lounge, vip-bathroom)
trigger "Ambient: Main Floor"
when always {
    if all(chance 20%, in rooms main_floor) {
        do spinner message ambientInterior
    }
}

# ------------------------------------------------------------------------------------------------------------------STATUS EFFECT TRIGGERS

trigger "Vogon Poetry Causes Nausea"
when use item vogon_poetry_book ability read {
    do show "You nearly get through the first poem when the vomiting begins..."
    do add flag read-vogon-poetry
    do add flag status:nauseated
    do award points -1
}

trigger "Status: Nauseated"
when always {
    if all(has flag status:nauseated, chance 50%) {
        do spinner message nauseated
    }
}

# ------------------------------------------------------------------------------------------------------------------GLOBAL (NO SPECIFIC LOCATION)

trigger "Global: Arson-Aid Used" only once
when use item flamethrower ability ignite {
    do show "The Initech Arson-Aid melts down into an amorphous, hissing blob. As expected."
}

trigger "Global: Open Delivery Box" only once
when open item delivery_box {
    do despawn item delivery_box
    do spawn item destroyed_box in current room
    do spawn item towel in current room
    do show "The box sighs and collapses in a heap as soon as you touch it, relieved that its mission is over. Inside, you find a nanotruss-reinforced tachyon-powered towel with an attached note:\n\n                Always be a frood who knows where his towel is.\n                                -- F.P."
}

trigger "Global: Take Vogon Poetry"
when take item vogon_poetry_book
{
    do show "A greasy feeling of nausea overcomes you."
}

trigger "Global: Drop Vogon Poetry"
when drop item vogon_poetry_book
{
    if any(missing flag read-vogon-poetry, has flag cured-vogon-poetry) {
        do show "That greasy, queasy feeling subsides."
    }
    if all(has flag read-vogon-poetry, missing flag cured-vogon-poetry) {
        do show "Getting rid of the book helps a little, but the poem is burned into your mind and the nausea continues."
    }
}

trigger "Global: Cannot Drop No Cake"
when drop item no_cake
{
    do despawn item no_cake
    do spawn item no_cake in inventory
    do show "Try as you might, you cannot rid yourself of the feeling of a missing, promised dessert. The cake was a lie."
}

# ATTACH TRIGGER PAIR  vvvvvv

trigger "Global: Fishing Line (attach clip to floss)"
when use item paper_clip on item dental_floss interaction attach
{
    do despawn item paper_clip
    do despawn item dental_floss
    do spawn item fishing_line in inventory
    do show "You MacGyvered the floss and paper clip into a fishing line!"
    do award points 3
}
trigger "Global: Fishing Line (attach floss to clip)"
when use item dental_floss on item paper_clip interaction attach
{
    do despawn item paper_clip
    do despawn item dental_floss
    do spawn item fishing_line in inventory
    do show "You MacGyvered the floss and paper clip into a fishing line!"
    do award points 3
}

# ATTACH TRIGGER PAIR ^^^^^^
